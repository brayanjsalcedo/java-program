plugins {
    id 'java'
    id 'org.springframework.boot' version "${springbootVersion}"
    id 'io.spring.dependency-management' version "${springbootDependencyManagementVersion}"
    id 'jacoco'
}

group = 'com.example.java.epam'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    integrationTestImplementation {
        extendsFrom testImplementation
    }

    integrationTestCompileOnly {
        extendsFrom testAnnotationProcessor
    }

    integrationTestAnnotationProcessor {
        extendsFrom testAnnotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-activemq'
    implementation 'org.flywaydb:flyway-core'
    implementation "com.itextpdf:itext7-core:${itextpdfVersion}"
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Configure JaCoCo for Unit Tests
jacocoTestReport {
    dependsOn test
    sourceDirectories.setFrom sourceSets.main.allJava.srcDirs
    classDirectories.setFrom sourceSets.main.output
    executionData.setFrom fileTree(dir: "$buildDir", includes: ["jacoco/test.exec"])

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }
}

// Configure JaCoCo for Integration Tests
tasks.register('integrationTest', Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Integration tests'
    maxHeapSize = '1024m'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    useJUnitPlatform()
    jacoco {
        destinationFile = file("$buildDir/jacoco/integrationTest.exec")
    }
}


tasks.register('jacocoIntegrationTestReport', JacocoReport) {
    dependsOn tasks.named('integrationTest')
    sourceDirectories.setFrom sourceSets.main.allJava.srcDirs
    classDirectories.setFrom sourceSets.main.output
    executionData.setFrom fileTree(dir: "$buildDir", includes: ["jacoco/integrationTest.exec"])

    reports {
        xml.required = true
        html.required = true
    }
}